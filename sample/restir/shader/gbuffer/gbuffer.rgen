#version 460
#extension GL_GOOGLE_include_directive : enable
#include "../common.glsl"

layout(location = 0) rayPayloadEXT HitPayload payload;

void main()
{
    // Calc ray
    const vec2 screenPos = vec2(gl_LaunchIDEXT.xy) + vec2(0.5, 0.5);
    const vec2 inUV = screenPos / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;

    vec4 origin    = pushConstants.invView * vec4(0, 0, 0, 1);
    vec4 target    = pushConstants.invProj * vec4(d.x, d.y, 1, 1) ;
    vec4 direction = pushConstants.invView * vec4(normalize(target.xyz), 0) ;
    
    payload.position = vec3(0.0);
    payload.normal = vec3(0.0);
    payload.diffuse = vec3(0.0);
    payload.emission = vec3(0.0);
    payload.instanceIndex = -1;
    traceRayEXT(
        topLevelAS,
        gl_RayFlagsOpaqueEXT,
        0xff, // cullMask
        0,    // sbtRecordOffset
        0,    // sbtRecordStride
        0,    // missIndex
        origin.xyz,
        0.001,
        direction.xyz,
        10000.0,
        0     // payloadLocation
    );
    imageStore(positionImage, ivec2(gl_LaunchIDEXT.xy), vec4(payload.position, 1.0));
    imageStore(normalImage, ivec2(gl_LaunchIDEXT.xy), vec4(payload.normal, 1.0));
    imageStore(diffuseImage, ivec2(gl_LaunchIDEXT.xy), vec4(payload.diffuse, 1.0));
    imageStore(emissionImage, ivec2(gl_LaunchIDEXT.xy), vec4(payload.emission, 1.0));
    imageStore(instanceIndexImage, ivec2(gl_LaunchIDEXT.xy), ivec4(payload.instanceIndex));
}
