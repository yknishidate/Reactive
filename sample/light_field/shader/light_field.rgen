#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_nonuniform_qualifier : enable

layout(location = 0) rayPayloadEXT vec3 payload;
layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D outputImage;
layout(push_constant) uniform PushConstants {
    mat4 invView;
    mat4 invProj;
    uint64_t vertices;
    uint64_t indices;
} constants;

void main()
{
    const vec2 screenPos = vec2(gl_LaunchIDEXT.xy);
    const vec2 xy = vec2(gl_LaunchIDEXT.xy) / vec2(gl_LaunchSizeEXT.xy) * 2.0 - 1.0;
    vec4 origin    = constants.invView * vec4(0, 0, 0, 1);
    vec4 target    = constants.invProj * vec4(xy.x, xy.y, 1, 1);
    vec4 direction = constants.invView * vec4(normalize(target.xyz), 0);

    traceRayEXT(
        topLevelAS,
        gl_RayFlagsOpaqueEXT,
        0xff, // cullMask
        0,    // sbtRecordOffset
        0,    // sbtRecordStride
        0,    // missIndex
        origin.xyz,
        0.001,
        direction.xyz,
        10000.0,
        0     // payloadLocation
    );
    imageStore(outputImage, ivec2(gl_LaunchIDEXT.xy), vec4(payload, 1));
}
